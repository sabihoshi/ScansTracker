{set;~replaces;{j;[
        {
            "bbtag": "b",
            "discord": "*"
        },
        {
            "bbtag": "u",
            "discord": "_"
        },
        {
            "bbtag": "spoiler",
            "discord": "|"
        },
        {
            "bbtag": "hr",
            "discord": "{newline}"
        }
]}}

{//; Encodes discord markdown into BBTag }
{function;encodeBBTag;
    {set;~result;{params}}

    {//; Url links }
    {set;~result;
        {regexreplace;{get;~result}
            ;/\[([^]]+?)\]\(([^]]+?)\)/ig;
            [url=$2]$1[/url]
        }
    }

    {//; Markdown formats }
    {foreach;~replace;~replaces;
        {set;~result;{inject;
            {lb}regexreplace
                {semi}{get;~result}
                {semi}/({repeat;\{jget;~replace;discord};2})(.+?)\1/ig
                {semi}[{jget;~replace;bbtag}]$2[/{jget;~replace;bbtag}]
            {rb}
        }}
    }

    {get;~result}
}

{//; Decodes BBTag into Discord compatible markdown }
{function;decodeBBTag;
    {set;~result;{params}}

    {//; Url links }
    {set;~result;
        {regexreplace;{get;~result}
            ;/\[url=([^]]+?)\]([^]]+?)\[\/url\]/ig;
            [$2]($1)
        }
    }

    {//; Markdown formats }
    {foreach;~replace;~replaces;
        {set;~result;{inject;
            {lb}regexreplace
                {semi}{get;~result}
                {semi}/\[\/?{jget;~replace;bbtag}\]/ig
                {semi}{repeat;{jget;~replace;discord};2}
            {rb}
        }}
    }

    {get;~result}
}